$(document).ready ->
  Cart.setup()
  Cart.setShipping()

window.Cart =
  setup: ->
    timer = null
    Cart.setShipping()
    $('#view_cart').on 'input', 'input[name*="[quantity]"]', (e) ->
      if timer then clearTimeout(timer)
      timer = setTimeout (=> 
          $.ajax
            url: $(@).parents('form').attr('action')
            data: $(@).parents('form').serialize()
            dataType: 'script'
            type: "POST"
            error: (jqXHR, textStatus, error) =>
              $(@).effect('highlight', {}, 2000)
              $(@).val($(@).data('quantity'))
        ), 600
    $('#shipping').on 'click', 'input[name="order[address_id]"]', (e) ->
      if timer then clearTimeout(timer)
      timer = setTimeout (=> 
          $.ajax
            url: $(@).parents('form').attr('action')
            data: $(@).parents('form').serialize()
            dataType: 'script'
            type: "POST"
        ), 600
    $('#current-total').on 'click', 'button.purchase', (e) ->
      e.preventDefault()
      if not $('#purchase').find('#order_address_id').val()
        noty 
          text: "Please choose shipping address."
          type: 'error'
          layout: 'topRight'
          timeout: 4000
      else if $('input[name*="order[items_attributes]"]').length is 0
        noty 
          text: "You should add product to cart before purchase."
          type: 'error'
          layout: 'topRight'
          timeout: 4000
      else
        $('#purchase').modal('show')
        $('#purchase').on 'shown', -> $(@).find('#order_guest_email').focus()

  update: (quantity = 0) ->
    cart = $('.navbar').find('.cart a')
    cart.stop(true, true).effect('pulsate', { times: 3 })
    cart.html("<i class=\"icon-shopping-cart icon-white\"></i> Cart (#{quantity})")
  
  shippingChanged: ->
    shipping = $('input[name="order[address_id]"]:checked').data('shipping-rate')
    shipping_for_total = $('#current-total').find('.shipping-rate').data('shipping-rate')

    shipping isnt shipping_for_total

  setSubTotal: (price = 0) ->
    $('#current-total').find('.sub-total').html(price)

  setShipping: ->
    checked = $('#shipping input[name="order[address_id]"]:checked')
    rate = checked.data('shipping-rate')
    shipping = $('#current-total').find('.shipping-rate')
    if shipping and rate
      shipping.html(rate)
      shipping.attr('data-shipping-rate', rate)

    if checked.length is 1 and not $('#order_address_id').val() > 0
      setTimeout (-> checked.click()), 100

  setTotal: (price = 0) ->
    $('#current-total').find('.total').html(price)